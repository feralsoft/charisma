import {call_with} from "../../utils.coil"
import {to_ast, to_html, compile} from "../../html-ast.coil"
import {parse_value} from "../../css-parser.coil"

def parse_val(str) = parse_value([nil str]).0

def rem_font_size() = getComputedStyle(document:documentElement)
  .:fontSize:split("px").0.pipe(parseInt)

def hex(s) = parseInt(s):toString(16):padStart(2 "0")
def from_hex(val) = parseInt(val 16)
def rem_to_px(val) = Number(val) * rem_font_size()
def px_to_rem(val) = Number(val) / rem_font_size()

let toggle_compile = {
  def :rgb_value({r g b}) = str("#" hex(r) hex(g) hex(b))
  def :hex_value({r g b}) = str("rgb(" from_hex(r) "," from_hex(g) "," from_hex(b) ")")
  def :rem_value({value}) = str(rem_to_px(value) "px")
  def :px_value({value}) = str(px_to_rem(value) "rem")
  def :var_value({name} elem)
    let rule_name = elem:closest("[data-kind=rule]").query("[data-attr=name]"):innerText:trim()
    return elem:closest("[data-kind=declaration]")
      .query("[data-attr=query]")
      .to_ast()
      .pipe(compile $$)
      .map(getComputedStyle Keyword:for(rule_name))
      .:next():value
  end
}

let enabled_nodes_query = toggle_compile.keys()
  .map(|kind| str("[data-kind=" kind "]"))
  .join(", ")
  .pipe(str:fmt(":is(" _ ")"))

def toggle_representation({signal, rebuild, root})
  for elem of root.query_all("[data-kind]:has(> [data-value])" enabled_nodes_query)
    signal:listen(elem "dblclick" |e| {
      e:stopPropagation()
      let node = elem.to_ast()
      let new_elem = node.pipe(:kind call_with(node, elem){...toggle_compile} parse_val to_html)
      elem:replaceWith(new_elem)
      rebuild(elem)
    })
  end
end

export default toggle_representation