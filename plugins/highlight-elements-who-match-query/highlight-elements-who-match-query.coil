import "./highlight-elements.css"
import {to_ast, compile, to_html} from "../../html-ast.coil"
import {parse_decl} from "../../css-parser.coil"

def highlight_elements_who_match_query({signal})
  signal:listen(window "click" |e| {
    if e:altKey
      document:styleSheets.0:rules
        .zip(0..)
        .filter(|[rule _]| e:target:matches(rule:selectorText))
        .reject(|[_ idx]| $("[data-css-index=\"" idx "\"]"))
        .each(|[rule idx]| {
          let html_node = to_html(parse_decl(rule:cssText).0)
          html_node:setAttribute("data-css-index", idx)
          $(".editor"):append(html_node)
          html_node:classList:add("newly-added")
          setTimeout(|| html_node:classList:remove("newly-added"), 1500)
        })
    end
  })

  for decl of $$("[data-kind=declaration]")
    let query_node = decl:querySelector("[data-attr=query]")
    let entered = false
    signal:listen(decl "mouseenter" |e| entered = true)
    signal:listen(decl "mouseleave" |e| entered = false)
    signal:listen(window "keydown" |e| {
      if e:ctrlKey if entered
        for elem of $$(query_node.pipe(_.to_ast() compile))
          elem:classList:add("css-query-hover")
        end
      end end
    })
    signal:listen(window "keyup" |e| {
      for elem of $$(query_node.pipe(_.to_ast() compile))
        elem:classList:remove("css-query-hover")
      end
    })
  end
end

export default highlight_elements_who_match_query
