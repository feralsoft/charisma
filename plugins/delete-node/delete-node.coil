import {parse_rule, parse_value} from "../../css-parser.coil"
import {to_html} from "../../html-ast.coil"
import "./delete-node.css"

let context_map = {
  def :declaration(text) = parse_rule([nil text])
  def :variable(text) = parse_value([nil text])
  def :rule(text) = parse_value([nil text])
}

protocol backspaced

-- IDEA: undo via storing the AST as an persistent immutable map
-- TODO: don't give people ability to backspace all the way to deleting a query
def delete_node({root, signal, rebuild})
  for node of root.query_all("[data-kind]")
    signal:listen(node "keydown" |e| {
      e:stopPropagation()
      if e:key == "Backspace" if node.backspaced
        let text_node = :div{
          class: "just-deleted-node"
          contenteditable: true
          "data-context" => node:parentNode:closest("[data-kind]"):dataset:kind
          def :onkeydown(e)
            if e:key == "Enter"
              let ast_node = context_map.(this:dataset:context)(this:innerText:trim()).0
                .log()
              this:replaceWith(to_html(ast_node))
              rebuild(root:closest("[data-kind=declaration]"))
            end
          end
        }
        node:replaceWith(text_node)
        text_node:focus()
        node.backspaced = false
        return
      end end
      if e:key == "Backspace"
        node.backspaced = true
      end
    })
  end
end

export default delete_node
