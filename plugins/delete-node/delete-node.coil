import {parse_rule, parse_value} from "../../css-parser.coil"
import {run} from "../../parse-utils.coil"
import {to_html} from "../../html-ast.coil"
import "./delete-node.css"

let context_map = {
  def :decl(text) = parse_rule.run(text)
  def :variable(text) = parse_value.run(text)
  def :rule(text) = parse_value.run(text)
} 

-- show warning when backspaced!!

-- TODO: use a data attribute instead of this protocol so we can style it
protocol backspaced

-- bug: why does it take 4-6 delete keys to delete a css rule from an already deleted value

-- IDEA: undo via storing the AST as an persistent immutable map
def delete_node({root, signal, rebuild, scope})
  let interval = nil
  root.query_all("[contenteditable]").each(|node| signal:listen(node "keydown" |e| {
    if !e:ctrlKey
      e:stopPropagation()
    end
  }))
  for node of root.query_all("[data-kind=decl] > [data-attr=rules] *:not([contenteditable])")
    signal:listen(node "keydown" |e| {
      if e:key == "Backspace" and node.backspaced
        if node:parentNode:closest("[data-kind]"):dataset:kind == "decl"
          node:remove()
          rebuild()
          return
        end
        let text_node = :div{
          class: scope:class(:deleted)
          contenteditable: true
          "data-context" => node:parentNode:closest("[data-kind]"):dataset:kind
          def :onkeydown(e)
            if e:key == "Enter"
              let ast_node = context_map.(this:dataset:context)(this:innerText:trim()).0
              this:replaceWith(to_html(ast_node))
              rebuild(root:closest("[data-kind=decl]"))
            end
          end
        }
        node:replaceWith(text_node)
        text_node:focus()
        node.backspaced = false
        return
      end
      if e:key == "Backspace"
        node.backspaced = true
        setTimeout(|| node.backspaced = false, 500)
      end
    })
  end
end

export default delete_node
